grammar of C++
<Program> ::= <PreprocessorDirective>* <Declaration>*

<PreprocessorDirective> ::= "#include" <IncludeFile> | "#ifndef" <Identifier> "#define" <Identifier> "..." "#endif"
<IncludeFile> ::= "<" <FileName> ">" | "\"" <FileName> "\""
<FileName> ::= [a-zA-Z_][a-zA-Z0-9_\.]* 

<Identifier> ::= [a-zA-Z_][a-zA-Z0-9_]* #!!special function name, e.g:~Vector()
<Literal> ::= <IntegerLiteral> | <FloatLiteral> | <BooleanLiteral> | <StringLiteral>
<IntegerLiteral> ::= [0-9]+
<FloatLiteral> ::= [0-9]+ "." [0-9]+
<BooleanLiteral> ::= "true" | "false" | "0" | "1" 
<StringLiteral> ::= "\"" [^"\n]* "\""

<Declaration> ::= <VariableDeclaration> | <FunctionDeclaration> | <ClassDeclaration> | <StructDeclaration> | <EnumDeclaration> | <TypedefDeclaration> | <NamespaceDeclaration> | <TemplateDeclaration>

<VariableDeclaration> ::= <Type> <Identifier> ";" | <Type> <Identifier> "=" <Expression> ";" #?? multiple declaration. e.g:int a, b = 0, c;
<FunctionDeclaration> ::= <Type> <Identifier> "(" <ParameterList>? ")" <FunctionBody>? | <Type> <Identifier> "(" <ParameterList>? ")" ";" #!! int main(){} matches <FunctionBody>, which lacks declaration and more.
<ClassDeclaration> ::= "class" <Identifier> (<AccessSpecifier> ":" <Identifier>)? "{" <ClassMember>* "}" #changed: derive added; !! Multiple derive, e.g:class Derived : public Base1, public Base2{}; Also, declaring a class variable is different from declaraing a class template.This may apply to <StructiDeclaration> and others too.
<StructDeclaration> ::= "struct" <Identifier> "{" <StructMember>* "}"
<EnumDeclaration> ::= "enum" <Identifier> "{" <EnumMember> ("," <EnumMember>)* "}"
<TypedefDeclaration> ::= "typedef" <Type> <Identifier> ";"
<NamespaceDeclaration> ::= "using" "namespace" <Identifier> "{" <Declaration>* "}" #changed
<TemplateDeclaration> ::= "template" "<" <TemplateParameterList> ">" <Declaration>

<ClassMember> ::= <VariableDeclaration> | <FunctionDeclaration> | <AccessSpecifier> ":" <ClassMember>* #!! Each AccessSpicifier may appear once at most.
<StructMember> ::= <VariableDeclaration> | <FunctionDeclaration>
<EnumMember> ::= <Identifier> ("=" <IntegerLiteral>)?

<AccessSpecifier> ::= "public" | "private" | "protected"

<Type> ::= "int" | "float" | "char" | "bool" | "void" | <ClassType> | <PointerType> | <ReferenceType> | <ArrayType> | <TemplateType>
<PointerType> ::= <Type> "*"
<ReferenceType> ::= <Type> "&"
<ArrayType> ::= <Type> "[" <IntegerLiteral> "]"
<ClassType> ::= <Identifier>
<TemplateType> ::= <Identifier> "<" <Type> ("," <Type>)* ">"

<ParameterList> ::= <Parameter> ("," <Parameter>)*
<Parameter> ::= <Type> <Identifier> | <Type> <Identifier> "=" <Expression> #changed

<TemplateParameterList> ::= <TemplateParameter> ("," <TemplateParameter>)*
<TemplateParameter> ::= "typename" <Identifier> | "class" <Identifier>

<FunctionBody> ::= "{" <Statement>* "}"

<Statement> ::= <ExpressionStatement> | <IfStatement> | <ForStatement> | <WhileStatement> | <DoWhileStatement> | <ReturnStatement> | <CompoundStatement> | <SwitchStatement> | <BreakStatement> | <ContinueStatement>
<ExpressionStatement> ::= <Expression> ";"
<IfStatement> ::= "if" "(" <Expression> ")" <Statement> ("else" <Statement>)?
<ForStatement> ::= "for" "(" <ExpressionStatement>? <Expression>? ";" <Expression>? ")" <Statement>
<WhileStatement> ::= "while" "(" <Expression> ")" <Statement>
<DoWhileStatement> ::= "do" <Statement> "while" "(" <Expression> ")" ";"
<ReturnStatement> ::= "return" <Expression>? ";"
<CompoundStatement> ::= "{" <Statement>* "}"
<SwitchStatement> ::= "switch" "(" <Expression> ")" "{" <SwitchCase>* <DefaultCase>? "}"
<SwitchCase> ::= "case" <Literal> ":" <Statement>* | "default" ":" <Statement>* 
<DefaultCase> ::="default" ":" <Statement>* 
<BreakStatement> ::= "break" ";"
<ContinueStatement> ::= "continue" ";"

<Expression> ::= <PrimaryExpression> | <Expression> <Operator> <Expression> | <UnaryOperator> <Expression> | <Expression> <PostfixOperator>
<PrimaryExpression> ::= <Identifier> | <Literal> | "(" <Expression> ")" | <FunctionCall> | <MemberAccess> | <PointerAccess>
<FunctionCall> ::= <Identifier> "(" <ArgumentList>? ")"
<MemberAccess> ::= <PrimaryExpression> "." <Identifier> ("(" <ArgumentList>? ")")?  #changed: () added, members may be functions
<PointerAccess> ::= <PrimaryExpression> "->" <Identifier> ("(" <ArgumentList>? ")")? #changed: () added
<ArgumentList> ::= <Expression> ("," <Expression>)*
<UnaryOperator> ::= "-" | "!" | "++" | "--" | "~" #changed: ~ added
<PostfixOperator> ::= "++" | "--"

<Operator> ::= "+" | "-" | "*" | "/" | "%" | "==" | "!=" | "<" | ">" | "<=" | ">=" | "&&" | "||" | "&" | "|" | "^" | "=" | "+=" | "-=" | "*=" | "/=" | "%=" | "<<" | ">>" #changed: & | ^ << >> added
